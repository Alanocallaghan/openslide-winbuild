diff -urN a/libssp/meson.build b/libssp/meson.build
--- a/libssp/meson.build	1969-12-31 19:00:00.000000000 -0500
+++ b/libssp/meson.build	2023-01-05 23:32:38.810202903 -0500
@@ -0,0 +1,67 @@
+conf = configuration_data({
+  'HAVE_HIDDEN_VISIBILITY': true,
+})
+
+sys_headers = [
+  'alloca.h',
+  'fcntl.h',
+  'limits.h',
+  'malloc.h',
+  'paths.h',
+  'stdio.h',
+  'stdlib.h',
+  'string.h',
+  'syslog.h',
+  'unistd.h',
+]
+sys_funcs = [
+  'memmove',
+  'mempcpy',
+  'strncat',
+  'strncpy',
+]
+cc = meson.get_compiler('c')
+foreach h : sys_headers
+  if cc.has_header(h)
+    conf.set('HAVE_' + h.to_upper().underscorify(), 1)
+  endif
+endforeach
+foreach f : sys_funcs
+  if cc.has_function(f)
+    conf.set('HAVE_' + f.to_upper().underscorify(), 1)
+  endif
+endforeach
+
+configure_file(
+  configuration : conf,
+  output : 'config.h',
+)
+
+subdir('ssp')
+
+ssp_sources = [
+  'gets-chk.c',
+  'memcpy-chk.c',
+  'memmove-chk.c',
+  'mempcpy-chk.c',
+  'memset-chk.c',
+  'snprintf-chk.c',
+  'sprintf-chk.c',
+  'ssp.c',
+  'stpcpy-chk.c',
+  'strcat-chk.c',
+  'strcpy-chk.c',
+  'strncat-chk.c',
+  'strncpy-chk.c',
+  'vsnprintf-chk.c',
+  'vsprintf-chk.c',
+]
+
+shared_library(
+  'ssp',
+  ssp_sources,
+  gnu_symbol_visibility : 'hidden',
+  install : true,
+  link_args : ['-Wl,--version-script=' + meson.current_source_dir() / 'ssp.map'],
+  version: '0.0.0',
+)
diff -urN a/libssp/ssp/meson.build b/libssp/ssp/meson.build
--- a/libssp/ssp/meson.build	1969-12-31 19:00:00.000000000 -0500
+++ b/libssp/ssp/meson.build	2023-01-05 22:52:02.645607503 -0500
@@ -0,0 +1,6 @@
+configure_file(
+  configuration : {
+    'ssp_have_usable_vsnprintf': 'undef',
+  },
+  output : 'ssp.h',
+)
diff -urN a/meson.build b/meson.build
--- a/meson.build	1969-12-31 19:00:00.000000000 -0500
+++ b/meson.build	2023-01-05 23:14:17.172892462 -0500
@@ -0,0 +1,8 @@
+project(
+  'libssp', 'c',
+  default_options : [
+    'buildtype=debugoptimized',
+  ]
+)
+
+subdir('libssp')
